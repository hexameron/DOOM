dnl Process this file with autoconf to produce a configure script.
dnl $Id: configure.in,v 1.4 1999/10/18 19:16:42 cphipps Exp $
AC_INIT(src/d_main.c)

dnl --- Check for system type
AC_CANONICAL_SYSTEM

case "$target" in
*-linux) AC_DEFINE(LINUX) ;;
*-linux-*) AC_DEFINE(LINUX) ;;
*-freebsd) AC_DEFINE(FREEBSD) ;;
esac

dnl --- Fire up automake
AM_INIT_AUTOMAKE(lxdoom, 1.4.1)
AM_CONFIG_HEADER(config.h)

dnl --- Checks for programs.
AC_PROG_CC
AM_PROG_AS as

dnl --- Checks for typedefs
AC_TYPE_SIZE_T

dnl --- Compiler characteristics
AC_C_INLINE
AC_C_BIGENDIAN

if test "$ac_cv_c_bigendian" != no
then AC_DEFINE(__BIG_ENDIAN__)
fi

AM_CONDITIONAL(I386_ASM, test "$NO" = yes)

dnl --- Libraries
dnl AC_PATH_X
AC_PATH_XTRA
AC_CHECK_LIB(m,sqrt)
AM_PATH_SDL(1.0.1, BUILD_SDLDOOM=lsdldoom, BUILD_SDLDOOM=)
MIXER_LIBS=-lSDL_mixer


dnl --- Library functions
AC_CHECK_FUNC(stricmp,,AC_DEFINE(stricmp,strcasecmp)) 
AC_CHECK_FUNC(strnicmp,,AC_DEFINE(strnicmp,strncasecmp)) 

dnl --- Check for joystick header
AC_CHECK_HEADERS(linux/joystick.h)

dnl --- Check for soundcard.h
BUILD_SNDSERV=
AC_CHECK_HEADERS(sys/soundcard.h,BUILD_SNDSERV=sndserv)
AC_CHECK_HEADERS(machine/soundcard.h,BUILD_SNDSERV=sndserv)
AC_CHECK_HEADERS(linux/soundcard.h,BUILD_SNDSERV=sndserv)

dnl --- Always use highres mode, unless explicitely --disabled
AC_ARG_ENABLE(highres,[  --disable-highres	  disables support for resulutions greater than 320x200],,AC_DEFINE(HIGHRES))
dnl --- By default, simple checks with little overhead are done to catch memory corruption, or
dnl --- problems with new code
AC_ARG_ENABLE(checks,[  --disable-checks	  disables some basic internal sanity checks],,
AC_DEFINE(SIMPLECHECKS)
AC_DEFINE(ZONEIDCHECK))
dnl --- Option to enable debugging
AC_ARG_ENABLE(debug,[  --enable-debug	  turns on various debugging features, like range checking and internal heap diagnostics],
AC_DEFINE(RANGECHECK)
AC_DEFINE(INSTRUMENTED)
AC_DEFINE(TIMEDIAG))
AC_ARG_ENABLE(heapcheck,[  --enable-heapcheck	  turns on continuous heap checking (very slow)],AC_DEFINE(CHECKHEAP))



AC_SUBST(BUILD_SDLDOOM)
AC_SUBST(SDL_CFLAGS)
AC_SUBST(MIXER_CFLAGS)
AC_SUBST(MIXER_LIBS)

dnl --- Pass install dirs to C pre-processor
dnl --- YUCK! but I can't see a better way to do this
if test "$prefix" = "NONE"
then prefix="/usr/local"
fi
if test "$exec_prefix" = "NONE"
then exec_prefix=$prefix
fi

dnl --- decide directories
gamesdir=$prefix/games
exp_gamesdir=`eval "echo $gamesdir"`
exp_datadir=`eval "echo $datadir"`
waddir=$exp_datadir/games/doom
dnl --- now finally spit them out
AC_DEFINE_UNQUOTED(SNDSERV_PATH,"$exp_gamesdir/sndserv")
AC_DEFINE_UNQUOTED(MUSSERV_PATH,"$exp_gamesdir/musserv")
AC_DEFINE_UNQUOTED(DOOMWADDIR,"$waddir")

dnl --- output
AC_OUTPUT(Makefile src/Makefile doc/Makefile data/Makefile)

dnl
dnl $Log: configure.in,v $
dnl Revision 1.4  1999/10/18 19:16:42  cphipps
dnl Version 1.4.1
dnl
dnl Revision 1.3  1999/10/12 15:42:50  cphipps
dnl Changed version to 1.4.0
dnl
dnl Revision 1.2  1999/10/02 11:43:37  cphipps
dnl Added autoconf options to control diagnostics
dnl
dnl Revision 1.1  1999/09/10 19:52:44  cphipps
dnl Initial revision
dnl
dnl
